apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


def dep = rootProject.ext.dependencies
def an = rootProject.ext.android

static def appName() {
    return "MrGank"
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getDefault())
}

android {
    compileSdkVersion an.compileSdkVersion
    buildToolsVersion an.buildToolsVersion
    defaultConfig {
        applicationId "com.f1reking.gank"
        minSdkVersion an.minSdkVersion
        targetSdkVersion an.targetSdkVersion
        versionCode 13
        versionName "1.3"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi', 'x86'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                 "$projectDir/schemas".toString()]
            }
        }
    }

    Properties props = new Properties()
    props.load(new FileInputStream(file("signing.properties")))
    signingConfigs {
        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                        def fileNames = "${appName()}_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}.apk"
                        outputFileName = fileNames
                    }
                }
            }
        }
    }

    kapt {
        generateStubs = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation project(":third-party")
    implementation dep.appcompatV7
    implementation dep.supportV4
    implementation dep.design
    implementation dep.recyclerview
    implementation dep.glide
    kapt dep.glideCompiler
    implementation dep.retrofit
    implementation dep.converterGson
    implementation dep.adapterRxjava2
    implementation dep.rxjava2
    implementation dep.rxandroid2
    implementation dep.gson
    implementation dep.okhttp3
    implementation dep.okhttp3Logging
    implementation dep.bugly
    implementation dep.multidex
    implementation dep.superAdapter
    implementation dep.PhotoView
    implementation dep.cardView
    implementation dep.XTabLayout
    implementation dep.bugly
    implementation dep.rxpermissions2
    implementation dep.materialsearchview
    debugImplementation dep.leakcanaryDebug
    releaseImplementation dep.leakcanaryRelease
    implementation dep.room
    kapt dep.roomCompiler
}
