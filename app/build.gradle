apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'io.fabric'

def dep = rootProject.ext.dependencies
def an = rootProject.ext.android

static def appName() {
    return "MrGank"
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getDefault())
}

android {
    compileSdkVersion an.compileSdkVersion
    buildToolsVersion an.buildToolsVersion
    defaultConfig {
        applicationId "com.f1reking.gank"
        minSdkVersion an.minSdkVersion
        targetSdkVersion an.targetSdkVersion
        versionCode 230
        versionName "2.3.0"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi'
        }
    }

    Properties props = new Properties()
    props.load(new FileInputStream(file("signing.properties")))
    signingConfigs {
        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                        packageApplication.outputDirectory = file("../apk/")
                        def fileNames = "${appName()}_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}.apk"
                        outputFileName = fileNames
                    }
                }
            }
        }
    }

    androidExtensions {
        experimental = true
    }

    greendao {
        //数据库的schema版本，也可以理解为数据库版本号
        schemaVersion 1
        //设置DaoMaster、DaoSession、Dao包名，也就是要放置这些类的包的全路径。
        daoPackage 'com.f1reking.gank.db.dao'
        //设置DaoMaster、DaoSession、Dao目录
        targetGenDir 'src/main/java'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation dep.appcompatV7
    implementation dep.supportV4
    implementation dep.design
    implementation dep.recyclerview
    implementation dep.glide
    implementation dep.retrofit
    implementation dep.converterGson
    implementation dep.adapterRxjava2
    implementation dep.rxjava2
    implementation dep.rxandroid2
    implementation dep.gson
    implementation dep.okhttp3
    implementation dep.okhttp3Logging
    implementation dep.multidex
    implementation dep.superAdapter
    implementation dep.PhotoView
    implementation dep.cardView
    implementation dep.XTabLayout
    implementation dep.rxpermissions2
    implementation dep.materialsearchview
    implementation dep.rxlifecycleKotlin
    implementation dep.rxlifecycleComponents
    implementation dep.StatusLayout
    implementation dep.swipebacklayout
    implementation dep.eventbus
    implementation dep.greenDao
    debugImplementation dep.leakcanaryDebug
    releaseImplementation dep.leakcanaryRelease
    kapt dep.glideCompiler
    implementation 'com.melnykov:floatingactionbutton:1.3.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.2@aar') {
        transitive = true
    }
    implementation 'com.safframework.log:saf-log:1.5.3'
}
